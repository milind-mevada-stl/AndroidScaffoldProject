apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'com.google.gms.google-services'

apply plugin: 'io.fabric'

apply plugin: 'com.google.firebase.firebase-perf'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.example.androidbasescaffoldapp"
        minSdkVersion 21
        targetSdkVersion 29
        // Change Only For Public Release
        def versionMajor = 1 // For Major Features
        def versionMinor = 0 // For Minor Features
        def versionPatch = 0 // For Bug Fixing and Patch

        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            manifestPlaceholders = [crashlyticsCollectionEnabled: "false"]
        }
        release {
            manifestPlaceholders = [crashlyticsCollectionEnabled: "true"]
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }
    flavorDimensions "api"
    productFlavors {
        development {
            dimension "api"
            applicationIdSuffix ".development"
            resValue "string", "app_name", "Base-Dev"
        }
        staging {
            dimension "api"
            applicationIdSuffix ".staging"
            resValue "string", "app_name", "Base-Staging"
        }
        production {
            dimension "api"
            resValue "string", "app_name", "Android-Base"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    def lifeCycleVersion = "2.1.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifeCycleVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifeCycleVersion"
    def liveDataKtxCVersion = "2.2.0-rc03"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$liveDataKtxCVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$liveDataKtxCVersion"

    //Sentry Error Logging
    implementation 'io.sentry:sentry-android:1.7.27'
    implementation 'org.slf4j:slf4j-nop:1.7.25'

    //Firebase
    implementation 'com.google.firebase:firebase-analytics:17.2.1'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    implementation 'com.google.firebase:firebase-config-ktx:19.1.0'
    implementation 'com.google.firebase:firebase-perf:19.0.4'
}
